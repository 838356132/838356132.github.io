
#!title:    理解阴阳谜题
#!date:     2018-02-11
#!authors:  Mikukonai
#!cover:    
#!type:     原创
#!tags:     Scheme,计算机科学,函数式编程

#!content

阴阳谜题是法国人David Madore设计的一段小程序，用来展示程序语言中“Continuation”的概念。这位大佬在自己设计的基于组合子的语言“Unlambda”的主页上写道：

> Your Functional Programming Language Nightmares Come True

emmm...阴阳谜题的确比较令人费解，难倒了不少初学者，当然也包括我。查阅了一些资料，结合自己使用脑内虚拟机仔细模拟之后，终于稍稍理解了一点。因此写下这篇文章，用来记录自己对此问题的解析和理解。

# 什么是阴阳谜题

阴阳谜题是一段短小的、无限循环的程序。其Scheme代码如下：

```:Scheme
(let* ((yin
         ((lambda (cc) (display #\@) cc) (call/cc (lambda (c) c))))
       (yang
         ((lambda (cc) (display #\\*) cc) (call/cc (lambda (c) c)))))
    (yin yang))
```

运行此程序，理论上会输出无穷长的“@\*..”序列。实际输出的序列长度，与实现阴阳谜题的语言和运行环境（例如栈空间）有关。

```
@*@**@***@****@*****@******@...
```

尽管阴阳谜题的代码十分短小，但却使用了两次`call/cc`，这就使程序的执行面貌变得难以理解。`call/cc`是Scheme的特色，它可以显式地捕获程序在某一位置的上下文环境，即continuation，并保存为一等continuation，供程序员随时调用。有了对一等continuation的支持，Scheme就可以随心所欲地控制程序的执行顺序，实现多重循环跳出、协程、异常处理等普通的递归程序难以实现的功能。从面向问题的角度看。continuation和`call/cc`是很容易理解的，因为它将程序控制流封装起来，是高度抽象的。但是从面向执行的角度看，程序的执行面貌，就变得复杂了许多。

下文从理解`call/cc`开始，逐步解析阴阳谜题的执行细节。

# 理解call/cc

call/cc，全称call-with-current-continuation，可以捕获程序运行到call/cc时候的continuation。在Racket或者Scheme中，`call/cc`接受一个单参函数作为它的参数，也就是`(call/cc (lambda (k) body))`的形式。之所以选用字母k，是因为`call/cc`会把取得的continuation传给`k`，然后在`body`中使用它。一旦`body`中**有调用**`k`，则整个call/cc返回的结果就是continuation的执行结果，而与`body`中的其他计算无关（因此这一特性可以用来打断点）；如果`body`中**没有调用**`k`，那么call/cc返回的结果就是`body`的值。

在[*Scheme and the art of programming*](http://www.doc88.com/p-7029536419237.html)一书中，以一整个章节讲了continuation和call/cc，据说是讲continuation讲得最透彻的一本书。此书将continuation的行为描述为一个“escaper”，相当贴切的一个词。根据各位逻辑学大佬的想法，在经典lambda演算中，是没有办法实现跳出动作的，所以说它很特殊。

我们可以利用call/cc和恒等函数，构造一个常用形式（如下），用来获取调用时刻的continuation：

```:Scheme
(call/cc (lambda (k) k))
```

有了这个常用形式之后，我们考虑下面这个例子，用来热身（[例子出处](https://www.scheme.com/tspl4/further.html)）：

```:Scheme
(((call/cc (lambda (x) x))
  (lambda (x) x)) "Hey")
```

其输出是“Hey”。这段代码中，用到了上面所说的特殊形式，于是整个程序的功能就是：用恒等函数去调整个程序的continuation，然后用结果去调“Hey”，其实也就是用整个程序的continuation去调“Hey”。当调用`call/cc`的时候，`call/cc`创造了一个新的环境，是这样的：

```:Scheme
((□
  (lambda (x) x)) "Hey")
```

把恒等函数塞进这个continuation挖的坑里面，就是

```:Scheme
(((lambda (x) x)
  (lambda (x) x)) "Hey")
```

到这里，就很容易知道结果是“Hey”了。后文中的分析，其实也是同样的方法，只是创造出来的平行世界多了一点而已。

维基百科还给了一个例子，我贴在这里：

```:Scheme
;; [LISTOF X] -> ( -> X u 'you-fell-off-the-end)
(define (generate-one-element-at-a-time lst)

  ;; Hand the next item from a-list to "return" or an end-of-list marker
  (define (control-state return)
    (for-each 
     (lambda (element)
               (set! return (call/cc
                              (lambda (resume-here)
                                ;; Grab the current continuation
                               (set! control-state resume-here)
                               (return element)))))
     lst)
    (return 'you-fell-off-the-end))
  
  ;; (-> X u 'you-fell-off-the-end)
  ;; This is the actual generator, producing one item from a-list at a time
  (define (generator)
    (call/cc control-state))

  ;; Return the generator 
  generator)

(define generate-digit
  (generate-one-element-at-a-time '(0 1 2)))

(generate-digit) ;; 0
(generate-digit) ;; 1
(generate-digit) ;; 2
(generate-digit) ;; you-fell-off-the-end
```

要理解这段程序的行为，首先要知道，顶级符号`generate-one-element-at-a-time`是一个闭包，里面维护着`lst`这个在作用域内有效的变量。还要知道，`for-each`函数可以遍历列表的每一个element，那么抽象地说，每次迭代的continuation都包含“尚未看到的”那部分列表。因此，可以使用call/cc获取每次遍历的continuation，动态地控制程序的流向。

当我们调用`generate-one-element-at-a-time`的时候，我们实际上是调用其内部的`generator`也就是`(call/cc control-state)`。我们知道，call/cc的返回值取决于`control-state`内部有没有调用它的参数`return`，我们发现，它确实调用了，并且返回的是当前看到的element。这个`return`的作用，就像命令式语言中的break或者continue（干脆就说return吧）。程序只要运行到`(return element)`，一下子就会跳出call/cc，所以会直接返回element。不考虑其他情况，如果遍历中遇到`(return element)`，那么遍历过程实际上并不能往前移动，因为第一次就会跳出循环。

在这个基础上，为了实现每次得到的值都不一样，既然每次都只能取得列表的第一个值，那么就可以在跳出遍历之前保存当前的continuation。这样，第二次调用`(call/cc control-state)`，实际上就是调用未来的、即将执行的、后续下一步的`control-state`。结合`generate-one-element-at-a-time`的闭包特性，就实现了每次调用都输出下一个值的功能。

---

还有一个例子。考虑比较容易理解的阶乘函数：

```:Scheme
(define retry #f)

(define factorial
  (lambda (x)
    (if (= x 0)
        (call/cc (lambda (k) (set! retry k) 1))
        (* x (factorial (- x 1))))))

(factorial 5) ;返回120，但是有副作用：将continuation保存在retry中
(retry 5)     ;返回600
```

程序运行的时候，从`(factorial 5)`开始，每一步递归都需要计算参数上的`(factorial n-1)`才能求值，于是调用栈上就有了一串`(factorial x)`。当程序运行到`(factorial 0)`的时候，call/cc被触发，当前的计算环境被保存在`retry`中。那么这个环境具体是怎样的呢？看下表

|Stack|Code waiting for<br>value of argument|
|----------|
|(factorial 0)|□|
|(factorial 1)|(* 1 ?)|
|(factorial 2)|(* 2 ?)|
|(factorial 3)|(* 3 ?)|
|(factorial 4)|(* 4 ?)|
|(factorial 5)|(* 5 ?)|

上表实际上就是执行到`(factorial 0)`（但尚未执行完）时由执行环境维护的调用栈，它在调call/cc的时候作为continuation的一部分被保存到`retry`了。表中的问号代表本步骤等待的值，□代表调call/cc时留下的传送门。如果没有call/cc，按照阶乘的算法，这里应该填入数字1；但是既然call/cc为我们留下了传送门，我们就可以自由地选择往这个坑里填什么东西。如果填5，意味着阶乘的初值就是5，执行此continuation也就是展开调用栈之后，得到的结果自然是600。

# 理解阴阳谜题

## 解析运行过程

原来的代码

```:Scheme
(let* ((yin  ((lambda (cc) (display #\@) cc) (call/cc (lambda (c) c))))
       (yang ((lambda (cc) (display #\\*) cc) (call/cc (lambda (c) c)))))
  (yin yang))
```

可以写成

```:Scheme
(let* ((yin  (identity@  (capture/cc1)))
       (yang (identity*  (capture/cc2))))
  (yin yang))
```

> 现在是我们熟悉的-1层世界

从第一对`let*`开始执行：

```:Scheme
;第0个CC：[C0]，是(capture/cc1)的返回值：
(let* ((yin  (identity@  □)) ; 挖掉call/cc后的剩余部分
       (yang (identity*  (capture/cc2))))
  (yin yang))

;(identity@  [C0])输出@，并得到[C0]
;此时的程序（0）
(let* ((yin  [C0])
       (yang (identity*  (capture/cc2))))
  (yin yang))

```

此时，yin已经被绑定为`[C0]`。

```:Scheme
;第1个CC：[C1]，是(capture/cc2)的返回值：
(let* ((yin  [C0])
       (yang (identity*  □))) ; 挖掉call/cc后的剩余部分
  (yin yang))
;注意，[C1]里面包含着[C0]

;(identity*  [C1])输出*，并得到[C1]
;此时的程序（1）
(let* ((yin  [C0])
       (yang [C1]))
  (yin yang))
```

接下来执行`(yin yang)`，也就是`([C0] [C1])`。

> 此时，我们通过`([C0] [C1])`传送门，来到了[C0]的世界

在[C0]的环境里，`yin`和`yang`还没有求值，需要重新求值。

```:Scheme
;[C0]如下，使用[C1]填补其中的坑
(let* ((yin  (identity@  [C1])) ; 填坑
       (yang (identity*  (capture/cc2))))
  (yin yang))

;执行(identity@  [C1])输出@，并得到[C1]
;此时的程序（2）
(let* ((yin  [C1])
       (yang (identity*  (capture/cc2))))
  (yin yang))
```

我们现在已经输出`@*@`了。

继续执行（2），会遇到[C0]里面的`(capture/cc2)`，它捕捉到另外一个新世界`[C2]`：

```:Scheme
;第2个CC：[C2]，是(capture/cc2)的返回值：
(let* ((yin  [C1])
       (yang (identity*  □))) ; 挖坑
  (yin yang))

;但我们仍然在[C0]的世界里，
;继续执行(identity*  [C2])，会输出一个*，并返回[C2]
;我们得到了程序（3）：
(let* ((yin  [C1])
       (yang [C2]))
  (yin yang))
```

我们现在已经输出`@*@*`了。

好了，现在`[C0]`的`yin`和`yang`都齐备了，分别是`[C1]`和`[C2]`。

因此接下来执行`(yin yang)`，也就是`([C1] [C2])`。

> 此时，我们通过`([C1] [C2])`传送门，来到了[C1]的世界

在[C1]的环境里，`yin`和`yang`还没有求值，需要重新求值。

```:Scheme
;[C1]如下，使用[C2]填补其中的坑
(let* ((yin  [C0])
       (yang (identity*  [C2]))) ; 填坑
  (yin yang))

;yin在原来的世界中生成[C1]的时候已经被绑定好了
;所以执行(identity*  [C2])输出*，并得到[C2]
;此时的程序（3）
(let* ((yin  [C0])
       (yang [C2]))
  (yin yang))
```

我们现在已经输出`@*@**`了。

现在，在`[C1]`中，`(yin yang)`就是`([C0] [C2])`。

按照同样的道理，接下来执行的是`([C2] [C3])`……至此，现象层面的道理已经解释清楚。

---

在Scheme语言中，`let\*`实际上是顺序执行的define的语法糖，它可以按顺序将多个变量（`yin`和`yang`）分别绑定到已经evaluated的值上面，或者反过来说。已经知道，对于application`(f x)`，应用序求值是先求`f`，再求`x`，从左向右求值，直到列表中所有表达式求值完毕，才进行application。因此，对于`(yin yang)`，也是先求值`yin`再求值`yang`，恰好和let\*的顺序一致。因此，可以将程序改写为更清楚（？）的形式：

```:Scheme
((identity@ (capture/cc1)) (identity* (capture/cc2)))

; 如果不考虑副作用，实际上就是
((capture/cc1) (capture/cc2))
```

思考一下调用的过程，大致可以画出这样的示意图：

```:Scheme
((CC1) (CC2))

[ CC1 | CC2 ]
  1|@   2|*
   |     |
[ C-0   C-1 ]
  □ X  C0 □
 3|@  \
  |   4\\*
[ C-1   C-2 ]
 C0 □  C1 □
  |   \
  |    5\\*
[ C-0   C-2 ]
  □ X  C1 □
  ...   ...
```

我的妈太抽象了……估计过不了几天连自己也看不懂了……

手写版：

![不要看左边的Cx……下图中，每一行代表一个configuration，左边是`yin`，右边是`yang`，Cx下面的两个东西代表Continuation里面的`yin`和`yang`两个变量的configuration，方框代表参数占位符（也就是在那个位置调用的`call/cc`），圈叉代表另一个`call/cc`。在阴阳谜题中，只有C0带有`call/cc`，这个圈叉就是无限循环的万恶之源。](./image/interpreter/yin-yang-puzzle.png)

简单总结一下：在程序运行中出现过的无数个Cx中，**只有C0可以产生新continuation**，并输出一个@。其余的虽然都包含C0，但是在application的时候，都只是通过`yang`转手，并且输出一颗星星。

例如，执行第一步`(C0 C1)`之后，C1被填充到C0的`yin`中，新生成的C2（其中`yin`是C1，`yang`是□，即包含C1）则被绑定到`yang`，于是第二步就是`(C1 C2)`，到这一步，没有C0的参与，C2被传送到C1的`yang`中，于是第三步就是`(C0 C2)`，又会生成一个C3，下一步application就是`(C2 C3)`…\*…`(C1 C3)`…\*…`(C0 C3)`…@…`(C3 C4)`…\*…`(C2 C4)`…\*…`(C1 C4)`…\*…`(C0 C4)`…@…`(C4 C5)`…\*…`(C3 C5)`…\*…`(C2 C5)`…\*…`(C1 C5)`…\*…`(C0 C5)`…@…`(C5 C6)`…\*…

这也就是说，每输出一个@，意味着一个新的continuation诞生了；每输出一颗星星，意味着已存在的continuation又转手了一次；每转手一次，`yin`指向的continuation就被脱掉一层。因为输出@的时候都会给`yang`指向的continuation包裹上一层，所以下一轮转手的次数就会多一次。

好像还是很复杂呀\~但愿过几个月后我还能看懂\~

## 等价的代码

参考资料[[5]](#参考资料)只使用函数而不是continuation来实现同样的功能，但是依然难以理解。我自己采用CPS风格写了一个等价的程序，以帮助理解。代码如下：

```:Scheme
(define (yinyang cont)
  (display "@")
  (cont)
  (yinyang (lambda () (cont) (display "*"))))

(yinyang (lambda () (display "*")))
```

这段代码还是比较容易理解的。实现此功能的方法有很多，这只是其中之一。

---

到这里，我觉得我已经可以说服我自己了。你觉得呢？

# 参考资料

- [[1] How-Does-the-Yin-Yang-Puzzle-Work](https://stackoverflow.com/questions/2694679/how-does-the-yin-yang-puzzle-work)
- [[2] Madore 的 call/cc](http://www.madore.org/~david/computers/callcc.html)
- [3] Springer G, Friedman D P. Scheme and the Art of Programming[M]. McGraw-Hill, Inc. 1989.
- [[4] Call-with-current-continuation](https://en.wikipedia.org/wiki/Call-with-current-continuation)
- [[5] Understanding the Yin-Yang Puzzle](https://www.cnblogs.com/cbscan/p/3746861.html)
- [[6] call/cc总结](http://www.sczyh30.com/posts/Functional-Programming/call-with-current-continuation/)
- [[7] Scheme语言深入](https://www.ibm.com/developerworks/cn/linux/l-schm/part3/)
- [[8] Scheme 程序语言介绍之一](https://www.ibm.com/developerworks/cn/linux/l-scheme/part2/index.html)

